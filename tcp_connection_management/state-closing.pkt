--tolerance_usecs=100000
0.000 socket(..., SOCK_STREAM, IPPROTO_TCP) = 3
0.000 setsockopt(3, SOL_SOCKET, SO_REUSEADDR, [1], 4) = 0
0.000 bind(3, ..., ...) = 0
0.000 listen(3, 1) = 0

+0 < S 0:0(0) win 65535 <mss 1460>
+0 > S. 0:0(0) ack 1 <...>
+0.1 < . 1:1(0) ack 1 win 65535
0.200 accept(3, ..., ...) = 4

// 服务端随便传输一点数据给客户端
+0.100 write(4, ..., 1000) = 1000
// 断言服务端会发出 1000 字节的数据
+0 > P. 1:1001(1000) ack 1 <...>

// 确认 1000 字节数据
+0.01 < . 1:1(0) ack 1001 win 257

// 服务端主动断开，会发送 FIN 给客户端
+.1 close(4) = 0
// 断言协议栈会发出 ACK 确认（服务端->客户端）
+0 > F. 1001:1001(0) ack 1 <...>

// 客户端在未对服务端的 FIN 做确认时，也发出 FIN 要求断开连接
+.1 < F. 1:1(0) ack 1001 win 257

// 断言协议栈会发出 ACK 确认（服务端->客户端）
+0 > . 1002:1002(0) ack 2 <...>

// 故意不回 ACK，让连接处于 CLOSING 状态
// +.100 < . 2:2(0) ack 1002 win 257

+0 `sleep 1000000`
